import LeanCourse.Common
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Deriv
import Mathlib.Analysis.SpecialFunctions.Integrals
import Mathlib.Analysis.Convolution
import Mathlib.Data.Real.Irrational
import Mathlib.MeasureTheory.Function.Jacobian
open BigOperators Function Set Real Topology
open MeasureTheory Interval Convolution ENNReal
noncomputable section
set_option linter.unusedVariables false
local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y)


/- # Today: Measure Theory and Integral Calculus

We cover the short chapter 11 from Mathematics in Lean. -/

/-
Last time we discussed differential analysis.
-/







example (a b : â„) : âˆ« x in a..b, x = (b ^ 2 - a ^ 2) / 2 :=
  integral_id

example (f : â„ â†’ â„) : â„ â†’ â„ := fun x â†¦
  âˆ« t in (0)..x, f t

example (a b : â„) : âˆ« x in a..b, exp x = exp b - exp a :=
  integral_exp

/- the notation `[[a, b]]` (in namespace `Interval`) means `uIcc a b`, i.e.
the interval from `min a b` to `max a b` -/
example {a b : â„} (h : (0 : â„) âˆ‰ [[a, b]]) : âˆ« x in a..b, 1 / x = log (b / a) :=
  integral_one_div h

example (a b : â„) : âˆ« x in a..b, exp (x + 3) = exp (b + 3) - exp (a + 3) := by
  rw [@intervalIntegral.integral_comp_add_right]
  exact integral_exp


/- We have the fundamental theorem of calculus in Lean. -/

example (f : â„ â†’ â„) (hf : Continuous f) (a b : â„) :
    deriv (fun u â†¦ âˆ« x : â„ in a..u, f x) b = f b :=
  (hf.integral_hasStrictDerivAt a b).hasDerivAt.deriv

example {f : â„ â†’ â„} {a b : â„} {f' : â„ â†’ â„} (h : âˆ€ x âˆˆ [[a, b]], HasDerivAt f (f' x) x)
    (h' : Continuous f') : âˆ« y in a..b, f' y = f b - f a :=
  intervalIntegral.integral_eq_sub_of_hasDerivAt h (h'.intervalIntegrable _ _)

/- We can use this to compute integrals if we know the antiderivative. -/

example (a b : â„) : âˆ« x in a..b, 2 * x * exp (x ^ 2) =
    exp (b ^ 2) - exp (a ^ 2) := by
  have h1 : âˆ€ x âˆˆ [[a, b]], HasDerivAt
    (fun x â†¦ exp (x ^ 2))
    (2 * x * exp (x ^ 2)) x
  Â· intro x hx
    rw [show 2 * x * exp (x ^ 2) = exp (x ^ 2) * (2 * x) by ring]
    apply HasDerivAt.comp (hâ‚‚ := exp) (h := fun x â†¦ x ^ 2)
    exact hasDerivAt_exp (x ^ 2)
    convert hasDerivAt_pow 2 x
    simp
  have h2 : IntervalIntegrable (fun x â†¦ 2 * x * exp (x ^ 2)) volume a b
  Â· apply Continuous.intervalIntegrable
    continuity
  exact intervalIntegral.integral_eq_sub_of_hasDerivAt h1 h2


/- If you `open Convolution`, you get the convolution of two functions. -/

example (f : â„ â†’ â„) (g : â„ â†’ â„) : f â‹† g = fun x â†¦ âˆ« t, f t * g (x - t) :=
  rfl

/- The measure of a set lives in the extended non-negative reals `[0, âˆ]`. -/
#check â„â‰¥0âˆ
example : â„â‰¥0âˆ = WithTop {x : â„ // 0 â‰¤ x} := rfl
example : (âˆ + 5) = âˆ := by simp
example : (âˆ * 0) = 0 := by simp

/- More generally, Mathlib develops Lebesgue integration, which requires measure theory.

The basic notion is that of a "Ïƒ-algebra".
A Ïƒ-algebra is called `MeasurableSpace` in Lean.
It consists of a collection of sets, called the *measurable* sets.
The empty set is measurable,
and the measurable sets are closed under complementation and countable unions. -/

variable {X : Type*} [MeasurableSpace X]

example : MeasurableSet (âˆ… : Set X) :=
  MeasurableSet.empty

example {s : Set X} (hs : MeasurableSet s) : MeasurableSet (sá¶œ) :=
  hs.compl

example {f : â„• â†’ Set X} (h : âˆ€ b, MeasurableSet (f b)) : MeasurableSet (â‹ƒ b, f b) :=
  MeasurableSet.iUnion h



/-
We now can define measures.

On paper, a measure on a set equipped with a Ïƒ-algebra
is a function from the measurable sets to `[0, âˆ]` that is additive on countable disjoint unions.

In Mathlib, we extend the measure to any set `s`
as the infimum of measures of measurable sets containing `s`.
Of course, many lemmas still require measurability assumptions, but not all.
-/

variable {Î¼ : Measure X}

example : Î¼ âˆ… = 0 :=
  measure_empty

example {s : â„• â†’ Set X} (hmeas : âˆ€ i, MeasurableSet (s i)) (hdis : Pairwise (Disjoint on s)) :
    Î¼ (â‹ƒ i, s i) = âˆ‘' i, Î¼ (s i) :=
  Î¼.m_iUnion hmeas hdis

example (s : Set X) : Î¼ s = â¨… (t âŠ‡ s) (h : MeasurableSet t), Î¼ t :=
  measure_eq_iInf s

example (s : â„• â†’ Set X) : Î¼ (â‹ƒ i, s i) â‰¤ âˆ‘' i, Î¼ (s i) :=
  measure_iUnion_le s

/- We say that a property `P` holds almost everywhere if the set of elements
where it doesn't hold has measure 0. -/
example {P : X â†’ Prop} : (âˆ€áµ x âˆ‚Î¼, P x) â†” Î¼ {x | Â¬ P x} = 0 :=
  Iff.rfl

/- Various spaces have a canonical measure associated to them, called `volume`. -/
example : MeasureSpace â„ := by infer_instance
#check (volume : Measure â„)

example (a b : â„) (h : a â‰¤ b) :
    (volume (Icc a b)).toReal = b - a := by
  simp [h]

#check volume_Icc

/- The collection of measurable sets on `â„` is the smallest Ïƒ-algebra containing the open sets. -/
example : BorelSpace â„ := by infer_instance

#check NullMeasurableSet

/- The collection of measurable sets on `â„` is the smallest Ïƒ-algebra containing the open sets.

Remark: `rw` will not rewrite inside a binder (like `fun x`, `âˆƒ x`, `âˆ« x` or `âˆ€á¶  x`). Use
`simp_rw`, `simp only` or `unfold` instead. -/
example : âˆ€áµ x : â„, Irrational x := by {
  unfold Irrational
  refine Countable.ae_not_mem ?h volume
  exact countable_range Rat.cast
}


/- A map is measurable if preimages of measurable sets under that map are measurable. -/
#print Measurable
#check Continuous.measurable

/- A map `f` into a normed group is integrable (rougly) when it is measurable and the map
  `x â†¦ â€–f xâ€–` has a finite integral. -/
#check Integrable

example : Â¬ Integrable (fun x â†¦ 1 : â„ â†’ â„) := by
  intro h
  rw [@integrable_const_iff] at h
  simp at h






/- We can take the integrals for functions intro a Banach space.
This version of the integral is called the *Bochner integral*.
The integral is denoted `âˆ« a, f x âˆ‚Î¼` -/
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E] {f : X â†’ E}

example {f g : X â†’ E} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    âˆ« x, f x + g x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ + âˆ« x, g x âˆ‚Î¼ :=
  integral_add hf hg


/-
* We can write `âˆ« x in s, f x âˆ‚Î¼` for the integral restricted to a set.
* In the following example, we compute the integral of a constant map
* The function `ENNReal.toReal` sends `âˆ` to `0`. -/
example {s : Set X} (c : E) : âˆ« x in s, c âˆ‚Î¼ = (Î¼ s).toReal â€¢ c :=
  set_integral_const c

/-
* We can abbreviate `âˆ« x, f x âˆ‚volume` to `âˆ« x, f x`
* We write `âˆ« x in a..b, f x âˆ‚Î¼` for the integral on an interval (whose sign is reversed if `b < a`)
-/
example {f : â„ â†’ E} {a b c : â„} : âˆ« x in a..b, c â€¢ f x = c â€¢ âˆ« x in a..b, f x :=
  intervalIntegral.integral_smul c f

example {f : â„ â†’ E} {a b : â„} : âˆ« x in b..a, f x = - âˆ« x in a..b, f x :=
  intervalIntegral.integral_symm a b

open Filter

/- Here is a version of the dominated convergence theorem. -/
example {F : â„• â†’ X â†’ E} {f : X â†’ E} (bound : X â†’ â„)
    (hmeas : âˆ€ n, AEStronglyMeasurable (F n) Î¼)
    (hint : Integrable bound Î¼) (hbound : âˆ€ n, âˆ€áµ x âˆ‚Î¼, â€–F n xâ€– â‰¤ bound x)
    (hlim : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n : â„• â†¦ F n x) atTop (ğ“ (f x))) :
    Tendsto (fun n â†¦ âˆ« x, F n x âˆ‚Î¼) atTop (ğ“ (âˆ« x, f x âˆ‚Î¼)) :=
  tendsto_integral_of_dominated_convergence bound hmeas hint hbound hlim


/- Here is the statement of Fubini's theorem. -/
variable {X Y : Type*} [MeasurableSpace X] {Î¼ : Measure X} [SigmaFinite Î¼]
    [MeasurableSpace Y] {Î½ : Measure Y} [SigmaFinite Î½] in
example (f : X Ã— Y â†’ E) (hf : Integrable f (Î¼.prod Î½)) :
    âˆ« z, f z âˆ‚ Î¼.prod Î½ = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼ :=
  integral_prod f hf

/- There is a version of the change of variables theorem. -/
example {s : Set â„} {f f' : â„ â†’ â„}
    (hs : MeasurableSet s)
    (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x)
    (hf : InjOn f s) (g : â„ â†’ E) : âˆ« x in f '' s, g x = âˆ« x in s, |f' x| â€¢ g (f x) :=
  integral_image_eq_integral_abs_deriv_smul hs hf' hf g


/- Here is a computation using the change of variables theorem. -/
example (f : â„ â†’ â„) :
    âˆ« x in (0)..Ï€, sin x * f (cos x) = âˆ« y in (-1)..1, f y := by
  rw [intervalIntegral.integral_of_le (by positivity),
      intervalIntegral.integral_of_le (by norm_num)]
  simp only [â† integral_Icc_eq_integral_Ioc]
  have : Icc (-1) 1 = cos '' Icc 0 Ï€
  Â· have := bijOn_cos
    exact (BijOn.image_eq this).symm
  rw [this, integral_image_eq_integral_abs_deriv_smul]
  rotate_left
  Â· exact measurableSet_Icc
  Â· intro x hx
    exact (hasDerivAt_cos x).hasDerivWithinAt
  Â· exact injOn_cos
  Â· simp
    apply set_integral_congr
    Â· exact measurableSet_Icc
    Â· intro x hx
      simp
      left
      symm
      simp
      exact sin_nonneg_of_mem_Icc hx

/- # Exercises

Feel free to either do the exercises, or work on your project.
-/

/- There are special cases of the change of variables theorems for affine transformations
  (but you can also use the change of variable theorem above) -/
example (a b : â„) :
    âˆ« x in a..b, sin (x / 2 + 3) =
    2 * cos (a / 2 + 3) - 2 * cos (b / 2 + 3) := by
  sorry

/- Use the change of variables theorem for this exercise. -/
example (f : â„ â†’ â„) (s : Set â„) (hs : MeasurableSet s) :
    âˆ« x in s, exp x * f (exp x) = âˆ« y in exp '' s, f y := by
  sorry

example (x : â„) : âˆ« t in (0)..x, t * exp t = (x - 1) * exp x + 1 := by
  sorry
