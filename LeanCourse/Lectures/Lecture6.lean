import LeanCourse.Common
open Set Function Real
noncomputable section
set_option linter.unusedVariables false
variable {Î¹ L : Type*}


/- # Today: Sets (continued) and functions

We cover sections 4.1, 4.2 and 4.3 from Mathematics in Lean.
Assignment 3 is posted and due on November 3. -/

/-
Last time we discussed negation `Â¬` (not), classical logic, sequences and sets.
-/

/- ## Proving two sets are equal

You can prove that two sets are equal by applying `subset_antisymm` or using the `ext` tactic.
-/


variable {Î± Î² : Type*} (x : Î±) (s t : Set Î±)

example : (fun x : â„ â†¦ x ^ 2) 3 = 10 := by
  simp only; sorry

/- We saw last time that we can prove that two sets are equal using `ext`. -/
example : s âˆ© t = t âˆ© s := by
  ext x
  simp only [mem_inter_iff, and_comm]

/- We can also use existing lemmas and `calc`. -/
example : (s âˆª tá¶œ) âˆ© t = s âˆ© t := by
  calc (s âˆª tá¶œ) âˆ© t
      = (s âˆ© t) âˆª (tá¶œ âˆ© t) := by rw [@inter_distrib_right]
    _ = (s âˆ© t) âˆª âˆ… := by rw [@compl_inter_self]
    _ = s âˆ© t := by rw [@union_empty]




/-
# Set-builder notation
-/

/- We can write `{x : Î± | p x}` to write the set of all elements in `Î±` where `p` holds. -/
def Evens : Set â„• := {n : â„• | Even n}
def Odds : Set â„• := {n | Â¬ Even n}

example : Evens âˆª Odds = univ := by
  ext n
  simp [Evens, Odds]
  exact em (Even n)




/- All set operators can be written using the set-builder notation. -/
example : s âˆ© t = {x | x âˆˆ s âˆ§ x âˆˆ t} := by rfl
example : s âˆª t = {x | x âˆˆ s âˆ¨ x âˆˆ t} := by rfl
example : s \ t = {x | x âˆˆ s âˆ§ x âˆ‰ t} := by rfl
example : sá¶œ = {x : Î± | x âˆ‰ s} := by rfl
example : (âˆ… : Set Î±) = {x | False} := by rfl
example : (univ : Set Î±) = {x | True} := by rfl


/-
# Other operations on sets
-/

/- We can take power sets.

-/
example (s : Set Î±) : ð’« s = {t : Set Î± | t âŠ† s} := by rfl -- \powerset

/- What is the type of `ð’« s`?
Answer: Set (Set Î±)
compare with set theory:
if `s âŠ† â„` then s âˆˆ ð’« â„ and ð’« s âˆˆ ð’« (ð’« â„)
-/


example (s t : Set Î±) : ð’« (s âˆ© t) = ð’« s âˆ© ð’« t := by
  ext; simp






/- We can take unions and intersections of families of sets in three different ways:
* Given a family of sets `C : Î¹ â†’ Set Î±`
  we can take the union and intersection of `C i`
  as `i` ranges over all elements of `Î¹`.
-/
example (C : Î¹ â†’ Set Î±) : â‹ƒ i : Î¹, C i = {x : Î± | âˆƒ i : Î¹, x âˆˆ C i} := by ext; simp

example (C : Î¹ â†’ Set Î±) : â‹‚ i : Î¹, C i = {x : Î± | âˆ€ i : Î¹, x âˆˆ C i} := by ext; simp

/-
* Given a family of sets `C : Î¹ â†’ Set Î±` and `s : Set Î¹`
  we can take the union and intersection of `C i`
  as `i` ranges over all elements in `s`.
-/
example (s : Set Î¹) (C : Î¹ â†’ Set Î±) : â‹ƒ i âˆˆ s, C i = {x : Î± | âˆƒ i âˆˆ s, x âˆˆ C i} := by ext; simp


/- Proof irrelevance: two proofs of the same proposition are equal. -/
example (s : Set Î¹) (i : Î¹) (h hâ‚‚ : i âˆˆ s) : h = hâ‚‚ := by
  rfl

example (s : Set Î¹) (C : Î¹ â†’ Set Î±) :
  â‹ƒ i : Î¹, â‹ƒ h : i âˆˆ s, C i = {x : Î± | âˆƒ i : Î¹, i âˆˆ s âˆ§ x âˆˆ C i} := by ext; simp


example (s : Set Î¹) (C : Î¹ â†’ Set Î±) : â‹‚ i âˆˆ s, C i = {x : Î± | âˆ€ i âˆˆ s, x âˆˆ C i} := by ext; simp

/-
* Given a collection of sets `C : Set (Set Î±)`
  we can take the union and intersection of `c`
  for all `c âˆˆ C`
-/

example (ð“’ : Set (Set Î±)) : â‹ƒâ‚€ ð“’ = {x : Î± | âˆƒ s âˆˆ ð“’, x âˆˆ s} := by rfl

example (ð“’ : Set (Set Î±)) : â‹‚â‚€ ð“’ = {x : Î± | âˆ€ s âˆˆ ð“’, x âˆˆ s} := by rfl

example (ð“’ : Set (Set Î±)) : â‹ƒâ‚€ ð“’ = â‹ƒ c âˆˆ ð“’, c := by ext; simp



example (C : Î¹ â†’ Set Î±) (s : Set Î±) : s âˆ© (â‹ƒ i, C i) = â‹ƒ i, (C i âˆ© s) := by
  ext x
  simp
  rw [@and_comm]


/- We can take images and preimages of sets.

`f â»Â¹' s` is the preimage of `s` under `f`.
`f '' s` is the image of `s` under `f`. -/

example (f : Î± â†’ Î²) (s : Set Î²) : f â»Â¹' s = { x : Î± | f x âˆˆ s } := by rfl

/- f '' s can also written as { f x | x âˆˆ s} -/
example (f : Î± â†’ Î²) (s : Set Î±) : { f x | x âˆˆ s} = { y : Î² | âˆƒ x âˆˆ s, f x = y } := by rfl


example {s : Set Î±} {t : Set Î²} {f : Î± â†’ Î²} : f '' s âŠ† t â†” s âŠ† f â»Â¹' t := by
  constructor
  Â· intro h x hx
    simp
    apply h
    exact mem_image_of_mem f hx
  Â· intro h y hy
    -- rw [mem_image] at hx
    obtain âŸ¨x, hx, rflâŸ© := hy
    -- subst y
    -- rw [â† hxy]
    specialize h hx
    simp at h
    exact h


/-
If you have a hypothesis `h : y = t` or `h : t = y`,
where `y` is a variable (and `t` anything),
then you can use `h` to substitute `y` by `t` everywhere, using the tactic `subst h` or `subst y`.
This can also be done by `obtain` and `intro` by naming the equality `rfl`.
-/


/- We have another name for `f '' univ`, namely `range f`. -/
example (f : Î± â†’ Î²) : f '' univ = range f := image_univ










/- We can do pointwise operations on sets. -/

open Pointwise

example (s t : Set â„) : s + t = {x : â„ | âˆƒ a b, a âˆˆ s âˆ§ b âˆˆ t âˆ§ a + b = x } := by rfl
example (s t : Set â„) : -s = {x : â„ | -x âˆˆ s } := by rfl

example : ({1, 3, 5} : Set â„) + {0, 10} = {1, 3, 5, 11, 13, 15} := by
  ext x
  simp [mem_add]
  norm_num
  tauto









/- # Exercises for the break. -/

example {f : Î² â†’ Î±} : f '' (f â»Â¹' s) âŠ† s := by sorry

example {f : Î² â†’ Î±} (h : Surjective f) : s âŠ† f '' (f â»Â¹' s) := by sorry

example {f : Î± â†’ Î²} (h : Injective f) : f '' s âˆ© f '' t âŠ† f '' (s âˆ© t) := by sorry

example {I : Type*} (f : Î± â†’ Î²) (A : I â†’ Set Î±) : (f '' â‹ƒ i, A i) = â‹ƒ i, f '' A i := by sorry

example : (fun x : â„ â†¦ x ^ 2) '' univ = { y : â„ | y â‰¥ 0 } := by sorry


/-
## Inverse of a function.

Suppose we have a function `f : Î± â†’ Î²`.
Can we find a inverse `g : Î² â†’ Î±` of `f`? Not without assuming that `f` is bijective...
But suppose we try, suppose that `âˆƒ x, f x = y`, and we want to define `g y`.
It must be one of the `x` such that `f x = y`.
We can choose one such `x` using *the axiom of choice*.
-/

section Inverse

variable (f : Î± â†’ Î²)

#check Classical.choose
#check Classical.choose_spec
open Classical

def conditionalInverse (y : Î²) (h : âˆƒ x : Î±, f x = y) :
   Î± :=
  Classical.choose h

lemma invFun_spec (y : Î²) (h : âˆƒ x, f x = y) :
    f (conditionalInverse f y h) = y :=
  Classical.choose_spec h

/- We can now define the function by cases on whether it lies in the range of `f` or not. -/

variable [Inhabited Î±]
def inverse (f : Î± â†’ Î²) (y : Î²) : Î± :=
  if h : âˆƒ x : Î±, f x = y then
    conditionalInverse f y h else
    default

local notation "g" => inverse f -- let's call this function `g`


/- We can now prove that `g` is a right-inverse if `f` is surjective
and a left-inverse if `f` is injective.
We use the `ext` tactic to show that two functions are equal. -/
example (hf : Surjective f) : f âˆ˜ g = id := by
  ext y
  simp
  obtain âŸ¨x, rflâŸ© := hf y
  simp [inverse, invFun_spec]


example (hf : Injective f) : g âˆ˜ f = id := by
  ext x
  simp [inverse]
  have h : âˆ€ x y : Î±, f x = f y â†” x = y
  Â· intro x y
    exact hf.eq_iff
  apply hf
  simp [invFun_spec]

end Inverse

/-
## Cantor's theorem

Let's prove Cantor's theorem: there is no surjective function from any set to its power set. -/

example : Â¬ âˆƒ (Î± : Type*) (f : Î± â†’ Set Î±), Surjective f := by sorry


/- In section 4.3 of MIL you can read the proof of the Cantor-SchrÃ¶der-Bernstein theorem. -/

example {f : Î± â†’ Î²} {g : Î² â†’ Î±}
    (hf : Injective f) (hg : Injective g) :
    âˆƒ h : Î± â†’ Î², Bijective h :=
  sorry -- see MIL
