import LeanCourse.Common
import Mathlib.Topology.Instances.Real
import Mathlib.MeasureTheory.Measure.Lebesgue.Basic
open BigOperators Function Set Filter Topology TopologicalSpace MeasureTheory
noncomputable section
set_option linter.unusedVariables false
local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y)


/- # Today: Topology

We cover chapter 9 from Mathematics in Lean. -/

/-
Last time we discussed abstract algebra.
-/







/- # Limits -/


/-
In topology, one of basic concepts is that of a limit.
Say `f : â„ â†’ â„`. There are many variants of limits.
* the limit of `f(x)` as `x` tends to `xâ‚€`
* the limit of `f(x)` as `x` tends to `âˆ` or `-âˆ`
* the limit of `f(x)` as `x` tends to `xâ‚€â»` or `xâ‚€âº`
* variations of the above with the additional assumption that `x â‰  xâ‚€`.

This gives 8 different notions of behavior of `x`.
Similarly, the value `f(x)` can have the same behavior:
`f(x)` tends to `âˆ`, `-âˆ`, `xâ‚€`, `xâ‚€âº`, ...

This gives `64` notions of limits.

When we prove that two limits compose: if
`f x` tends to `yâ‚€` when `x` tends to `xâ‚€` and
`g y` tends to `zâ‚€` when `y` tends to `yâ‚€` then
`(g âˆ˜ f) x` tends to `zâ‚€` when `x` tends to `xâ‚€`.
This lemma has 512 variants.

Obviously we don't want to prove this 512 times.
Solution: use filters.










If `X` is a type, a filter `F : Filter X` is a
collection of sets `F.sets : Set (Set X)` satisfying the following:
-/
section Filter

variable {X Y : Type*} (F : Filter X)

#check (F.sets : Set (Set X))
#check (F.univ_sets : univ âˆˆ F.sets)
#check (F.sets_of_superset : âˆ€ {U V},
  U âˆˆ F.sets â†’ U âŠ† V â†’ V âˆˆ F.sets)
#check (F.inter_sets : âˆ€ {U V},
  U âˆˆ F.sets â†’ V âˆˆ F.sets â†’ U âˆ© V âˆˆ F.sets)
end Filter






/-
Examples of filters:
-/

/- `(atTop : Filter â„•)` is made of sets of `â„•` containing
`{n | n â‰¥ N}` for some `N` -/
#check (atTop : Filter â„•)

/- `ğ“ x`, made of neighborhoods of `x` in a topological space -/
#check (ğ“ 3 : Filter â„)

/- `Î¼.ae` is made of sets whose complement has zero measure with respect to a measure `Î¼`. -/
#check (volume.ae : Filter (â„ Ã— â„ Ã— â„))

/-
It may be useful to think of a filter on a type `X`
as a generalized element of `Set X`.
* `atTop` is the "set of very large numbers"
* `ğ“ xâ‚€` is the "set of points very close to `xâ‚€`."
* For each `s : Set X` we have the so-called *principal filter*
  `ğ“Ÿ s` consisting of all sets that contain `s` (exercise!).
-/






/- Operations on filters -/

/- the *pushforward* of filters generalizes images of sets. -/
example {X Y : Type*} (f : X â†’ Y) : Filter X â†’ Filter Y :=
  Filter.map f

example {X Y : Type*} (f : X â†’ Y) (F : Filter X) (V : Set Y) :
    V âˆˆ Filter.map f F â†” f â»Â¹' V âˆˆ F := by
  rfl

/- the *pullback* of filters generalizes preimages -/
example {X Y : Type*} (f : X â†’ Y) : Filter Y â†’ Filter X :=
  Filter.comap f

/- These form a *Galois connection* / adjunction -/
example {X Y : Type*} (f : X â†’ Y) (F : Filter X) (G : Filter Y) :
    Filter.map f F â‰¤ G â†” F â‰¤ Filter.comap f G := by
  exact?

/- `Filter X` has an order that turns it into a complete lattice. The order is reverse inclusion: -/
example {X : Type*} (F F' : Filter X) :
    F â‰¤ F' â†” âˆ€ V : Set X, V âˆˆ F' â†’ V âˆˆ F := by
  rfl

/- This makes the principal filter `ğ“Ÿ : Set X â†’ Filter X` monotone. -/
example {X : Type*} : Monotone (ğ“Ÿ : Set X â†’ Filter X) := by
  exact?







/- Using these operations, we can define the limit. -/
def MyTendsto {X Y : Type*} (f : X â†’ Y)
    (F : Filter X) (G : Filter Y) :=
  map f F â‰¤ G

def Tendsto_iff {X Y : Type*} (f : X â†’ Y)
    (F : Filter X) (G : Filter Y) :
    Tendsto f F G â†” âˆ€ S : Set Y, S âˆˆ G â†’ f â»Â¹' S âˆˆ F := by
  rfl

/- A sequence `u` converges to `x` -/
example (u : â„• â†’ â„) (x : â„) : Prop :=
  Tendsto u atTop (ğ“ x)

/- `\lim_{x â†’ xâ‚€} f(x) = yâ‚€` -/
example (f : â„ â†’ â„) (xâ‚€ yâ‚€ : â„) : Prop :=
  Tendsto f (ğ“ xâ‚€) (ğ“ yâ‚€)

/- `\lim_{x â†’ xâ‚€â», x â‰  xâ‚€} f(x) = yâ‚€âº` -/
example (f : â„ â†’ â„) (xâ‚€ yâ‚€ : â„) : Prop :=
  Tendsto f (ğ“[<] xâ‚€) (ğ“[â‰¥] yâ‚€)

/- Now the following states all possible composition lemmas all at
once! -/
example {X Y Z : Type*} {F : Filter X} {G : Filter Y} {H : Filter Z}
    {f : X â†’ Y} {g : Y â†’ Z}
    (hf : Tendsto f F G) (hg : Tendsto g G H) :
    Tendsto (g âˆ˜ f) F H := by
  sorry /- # Exercise -/

/-
Filters also allow us to reason about things that are
"eventually true". If `F : Filter X` and `P : X â†’ Prop` then
`âˆ€á¶  n in F, P n` means that `P n` is eventually true for `n` in `F`.
It is defined to be `{ x | P x } âˆˆ F`.

The following example shows that if `P n` and `Q n` hold for
sufficiently large `n`, then so does `P n âˆ§ Q n`.
-/
example (P Q : â„• â†’ Prop)
    (hP : âˆ€á¶  n in atTop, P n)
    (hQ : âˆ€á¶  n in atTop, Q n) :
    âˆ€á¶  n in atTop, P n âˆ§ Q n :=
  hP.and hQ








section Topology

/- Let's look at the definition of topological space. -/

variable {X : Type*} [TopologicalSpace X]
variable {Y : Type*} [TopologicalSpace Y]


example {Î¹ : Type*} (s : Î¹ â†’ Set X) : interior (â‹‚ i, s i) âŠ† â‹‚ i, interior (s i) := by
  sorry

/- A map between topological spaces is continuous if the
preimages of open sets are open. -/
example {f : X â†’ Y} :
    Continuous f â†” âˆ€ s, IsOpen s â†’ IsOpen (f â»Â¹' s) :=
  continuous_def

/- It is equivalent to saying that for any `xâ‚€` the function
value `f x` tends to `f xâ‚€` whenever `x` tends to `xâ‚€`. -/
example {f : X â†’ Y} :
    Continuous f â†” âˆ€ xâ‚€, Tendsto f (ğ“ xâ‚€) (ğ“ (f xâ‚€)) := by
  exact?

/- By definition, the right-hand side states that `f` is
continuous at `xâ‚€`. -/
example {f : X â†’ Y} {xâ‚€ : X} :
    ContinuousAt f xâ‚€ â†” Tendsto f (ğ“ xâ‚€) (ğ“ (f xâ‚€)) := by
  rfl







/- Neighborhoods are characterized by the following lemma. -/
example {x : X} {s : Set X} :
    s âˆˆ ğ“ x â†” âˆƒ t, t âŠ† s âˆ§ IsOpen t âˆ§ x âˆˆ t :=
  mem_nhds_iff

example {x : X} {s : Set X} (h : s âˆˆ ğ“ x) : x âˆˆ s := by
  sorry






/- We can state that a topological space satisfies
  separatedness axioms. -/

example : T0Space X â†” Injective (ğ“ : X â†’ Filter X) := by
  exact?

example : T1Space X â†” âˆ€ x, IsClosed ({ x } : Set X) :=
  âŸ¨by exact?, by exact?âŸ©

example : T2Space X â†”
    âˆ€ x y : X, x â‰  y â†’ Disjoint (ğ“ x) (ğ“ y) := by
  exact?

example : RegularSpace X â†” âˆ€ {s : Set X} {a},
    IsClosed s â†’ a âˆ‰ s â†’ Disjoint (ğ“Ë¢ s) (ğ“ a) := by
  exact?










/- A set is compact if every open cover has a finite subcover. -/

example {K : Set X} : IsCompact K â†” âˆ€ {Î¹ : Type _}
    (U : Î¹ â†’ Set X), (âˆ€ i, IsOpen (U i)) â†’ (K âŠ† â‹ƒ i, U i) â†’
    âˆƒ t : Finset Î¹, K âŠ† â‹ƒ i âˆˆ t, U i := by
  exact?

/-
This can also be reformulated using filters.
* `NeBot F` iff `F â‰  âŠ¥` iff `âˆ… âˆ‰ F`
* `ClusterPt x F` means that `F` has nontrivial intersection
  with `ğ“ x` (viewed as a generalized sets).
* `K` is compact iff every nontrivial filter that contains `K`
  has a cluster point in `K`.
-/

example (F : Filter X) : NeBot F â†” F â‰  âŠ¥ := by
  exact?

example {x : X} (F : Filter X) :
    ClusterPt x F â†” NeBot (ğ“ x âŠ“ F) := by
  rfl

example {K : Set X} : IsCompact K â†”
    âˆ€ {F} [NeBot F], F â‰¤ ğ“Ÿ K â†’ âˆƒ x âˆˆ K, ClusterPt x F := by
  rfl

end Topology














section Metric

variable {X Y : Type*} [MetricSpace X] [MetricSpace Y]

/- A metric space is a type `X` equipped with a distance function `dist : X â†’ X â†’ â„` with the following properties. -/

#check (dist : X â†’ X â†’ â„)
#check (dist_nonneg : âˆ€ {a b : X}, 0 â‰¤ dist a b)
#check (dist_eq_zero : âˆ€ {a b : X}, dist a b = 0 â†” a = b)
#check (dist_comm : âˆ€ (a b : X), dist a b = dist b a)
#check (dist_triangle : âˆ€ (a b c : X), dist a c â‰¤ dist a b + dist b c)

/- In metric spaces, all topological notions are also
characterized by the distance function. -/

example (f : X â†’ Y) (xâ‚€ : X) : ContinuousAt f xâ‚€ â†”
    âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {x},
    dist x xâ‚€ < Î´ â†’ dist (f x) (f xâ‚€) < Îµ :=
  Metric.continuousAt_iff

example (x : X) (Îµ : â„) :
    Metric.ball x Îµ = { y | dist y x < Îµ } := by
  rfl

example (s : Set X) :
    IsOpen s â†” âˆ€ x âˆˆ s, âˆƒ Îµ > 0, Metric.ball x Îµ âŠ† s :=
  Metric.isOpen_iff

end Metric




/- # Exercises

The goal of these exercise is to prove that
the regular open sets in a topological space form a complete boolean algebra.
`U âŠ” V` is given by `interior (closure (U âˆª V))`.
`U âŠ“ V` is given by `U âˆ© V`. -/


variable {X : Type*} [TopologicalSpace X]

variable (X) in
structure RegularOpens where
  carrier : Set X
  isOpen : IsOpen carrier
  regular' : interior (closure carrier) = carrier

namespace RegularOpens

/- We write some lemmas so that we can easily reason about regular open sets. -/
variable {U V : RegularOpens X}

instance : SetLike (RegularOpens X) X where
  coe := RegularOpens.carrier
  coe_injective' := fun âŸ¨_, _, _âŸ© âŸ¨_, _, _âŸ© _ => by congr

theorem le_def {U V : RegularOpens X} : U â‰¤ V â†” (U : Set X) âŠ† (V : Set X) := by simp
@[simp] theorem regular {U : RegularOpens X} : interior (closure (U : Set X)) = U := U.regular'

@[simp] theorem carrier_eq_coe (U : RegularOpens X) : U.1 = â†‘U := rfl

@[ext] theorem ext (h : (U : Set X) = V) : U = V :=
  SetLike.coe_injective h


/- First we want a complete lattice structure on the regular open sets.
We can obtain this from a so-called `GaloisCoinsertion` with the closed sets.
This is a pair of maps
* `l : RegularOpens X â†’ Closeds X`
* `r : Closeds X â†’ RegularOpens X`
with the properties that
* for any `U : RegularOpens X` and `C : Closeds X` we have `l U â‰¤ C â†” U â‰¤ r U`
* `r âˆ˜ l = id`
If you know category theory, this is an *adjunction* between orders
(or more precisely, a coreflection).
-/

/- The closure of a regular open set. Of course mathlib knows that the closure of a set is closed.
(the `simps` attribute will automatically generate the simp-lemma for you that
`(U.cl : Set X) = closure (U : Set X)`
-/
@[simps]
def cl (U : RegularOpens X) : Closeds X :=
  âŸ¨closure U, sorryâŸ©

/- The interior of a closed set. You will have to prove yourself that it is regular open. -/
@[simps]
def _root_.TopologicalSpace.Closeds.int (C : Closeds X) : RegularOpens X :=
  âŸ¨interior C, sorry, sorryâŸ©

/- Now let's show the relation between these two operations. -/
lemma cl_le_iff {U : RegularOpens X} {C : Closeds X} :
    U.cl â‰¤ C â†” U â‰¤ C.int := by sorry

@[simp] lemma cl_int : U.cl.int = U := by sorry

/- This gives us a GaloisCoinsertion. -/

def gi : GaloisCoinsertion cl (fun C : Closeds X â†¦ C.int) where
  gc U C := cl_le_iff
  u_l_le U := by simp
  choice C hC := C.int
  choice_eq C hC := rfl

/- It is now a general theorem that we can lift the complete lattice structure from `Closeds X`
to `RegularOpens X`. The lemmas below give the definitions of the lattice operations. -/

instance completeLattice : CompleteLattice (RegularOpens X) :=
  GaloisCoinsertion.liftCompleteLattice gi

@[simp] lemma coe_inf {U V : RegularOpens X} : â†‘(U âŠ“ V) = (U : Set X) âˆ© V := by
  have : U âŠ“ V = (U.cl âŠ“ V.cl).int := rfl
  simp [this]

@[simp] lemma coe_sup {U V : RegularOpens X} : â†‘(U âŠ” V) = interior (closure ((U : Set X) âˆª V)) := by
  have : U âŠ” V = (U.cl âŠ” V.cl).int := rfl
  simp [this]

@[simp] lemma coe_top : ((âŠ¤ : RegularOpens X) : Set X) = univ := by
  have : (âŠ¤ : RegularOpens X) = (âŠ¤ : Closeds X).int := rfl
  simp [this]

@[simp] lemma coe_bot : ((âŠ¥ : RegularOpens X) : Set X) = âˆ… := by
  have : (âŠ¥ : RegularOpens X) = (âŠ¥ : Closeds X).int := rfl
  simp [this]

@[simp] lemma coe_sInf {U : Set (RegularOpens X)} :
    ((sInf U : RegularOpens X) : Set X) =
    interior (â‹‚â‚€ ((fun u : RegularOpens X â†¦ closure u) '' U)) := by
  have : sInf U = (sInf (cl '' U)).int := rfl
  simp [this]

@[simp] lemma Closeds.coe_sSup {C : Set (Closeds X)} : ((sSup C : Closeds X) : Set X) =
    closure (â‹ƒâ‚€ ((â†‘) '' C)) := by
  have : sSup C = Closeds.closure (sSup ((â†‘) '' C)) := rfl
  simp [this]; rfl

@[simp] lemma coe_sSup {U : Set (RegularOpens X)} :
    ((sSup U : RegularOpens X) : Set X) =
    interior (closure (â‹ƒâ‚€ ((fun u : RegularOpens X â†¦ closure u) '' U))) := by
  have : sSup U = (sSup (cl '' U)).int := rfl
  simp [this]

/- We still have to prove that this gives a distributive lattice.
Note: these are hard; you might want to do the next exercises first. -/
instance completeDistribLattice : CompleteDistribLattice (RegularOpens X) :=
  { completeLattice with
    inf_sSup_le_iSup_inf := by sorry
    iInf_sup_le_sup_sInf := by sorry
    }


instance : HasCompl (RegularOpens X) := sorry


@[simp]
lemma coe_compl (U : RegularOpens X) : â†‘Uá¶œ = interior (U : Set X)á¶œ := by sorry


instance : CompleteBooleanAlgebra (RegularOpens X) :=
  { completeDistribLattice,
    inferInstanceAs (DistribLattice (RegularOpens X)) with
    inf_compl_le_bot := by sorry
    top_le_sup_compl := by sorry }
